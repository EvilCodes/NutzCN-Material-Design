apply plugin: 'com.android.application'

// 根据日期自动生成build号
def calendar = Calendar.getInstance();
def tt = String.format("%d%02d%02d%02d",
        calendar.get(Calendar.YEAR),
        calendar.get(Calendar.MONTH)+1,
        calendar.get(Calendar.DAY_OF_MONTH),
        calendar.get(Calendar.HOUR_OF_DAY));

// 读取local.properties文件
def Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    signingConfigs {
        NutzCN {
            //
            keyAlias 'nutzcn'
            keyPassword properties.getProperty("key.password")
            storeFile file('nutzcn.keystore')
            storePassword properties.getProperty("key.password")
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    defaultConfig {
        applicationId "net.wendal.nutzbook"
        minSdkVersion 17
        targetSdkVersion 21
        versionName "3.2.0"
        manifestPlaceholders = [
                UMENG_CHANNEL: "NutzCN",
                UMENG_APPKEY : "5625df2367e58ea9bc002d22",
                UMENG_MESSAGE_SECRET : "ef3c4434164a75b7b57312e5745eddab",
                JPUSH_CHANNEL: "NutzCN",
                JPUSH_APPKEY : "2d5a720c88aeb352eb9b1089"
        ]
        signingConfig signingConfigs.NutzCN
        versionCode Integer.parseInt(tt)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.NutzCN
        }
        debug {
            signingConfig signingConfigs.NutzCN
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    repositories {
        maven { url "https://jitpack.io" }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:design:23.1.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'joda-time:joda-time:2.8.1'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.5.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.github.rey5137:material:1.2.1'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile('com.afollestad.material-dialogs:core:0.8.3.0@aar') { transitive = true }
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'com.dlazaro66.qrcodereaderview:qrcodereaderview:1.0.0'
    compile 'org.tautua.markdownpapers:markdownpapers-core:1.4.2'
    compile project(':umeng-update-sdk')
    compile 'com.umeng.analytics:analytics:latest.integration'
    //image picker
    compile project(':android-image-picker-master');
}
